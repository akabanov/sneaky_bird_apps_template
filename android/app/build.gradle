plugins {
    id "com.android.application"
    id "kotlin-android"
    // The Flutter Gradle Plugin must be applied after the Android and Kotlin Gradle plugins.
    id "dev.flutter.flutter-gradle-plugin"
}

def codeSignKeystoreFile = file(System.getenv('PLAY_CONSOLE_UPLOAD_KEYSTORE'))
def codeSignKeystorePassFile = file(System.getenv('PLAY_CONSOLE_UPLOAD_KEYSTORE_PASS'))
def sign = codeSignKeystoreFile.isFile() && codeSignKeystorePassFile.isFile()
def keystorePass = sign ? codeSignKeystorePassFile.text.trim() : ''

android {
    namespace = "com.example.sneaky_bird_apps_template"
    compileSdk = flutter.compileSdkVersion
    ndkVersion = flutter.ndkVersion

    compileOptions {
        sourceCompatibility = JavaVersion.VERSION_1_8
        targetCompatibility = JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_1_8
    }

    defaultConfig {
        applicationId = "com.example.sneaky_bird_apps_template"
        // You can update the following values to match your application needs.
        // For more information, see: https://flutter.dev/to/review-gradle-config.
        minSdk = flutter.minSdkVersion
        targetSdk = flutter.targetSdkVersion
        versionCode = flutter.versionCode
        versionName = flutter.versionName
        // custom
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    if (sign) {
        signingConfigs {
            release {
                keyAlias = 'upload'
                keyPassword = keystorePass
                storeFile = codeSignKeystoreFile
                storePassword = keystorePass
            }
        }
    }

    buildTypes {
        release {
            signingConfig = sign ? signingConfigs.release : signingConfigs.debug
        }
    }
}

dependencies {
    testImplementation 'junit:junit:4.12'

    // https://developer.android.com/jetpack/androidx/releases/test/#1.2.0
    //noinspection GradleDependency
    androidTestImplementation 'androidx.test:runner:1.2.0'
    //noinspection GradleDependency
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'
}

flutter {
    source = "../.."
}
